module GMPZ;

type MPZ = unbox struct { _data : Destructor Ptr };

namespace MPZ {

    _make : () -> MPZ;
    _make = |_| (
        let ptr = CALL_C[Ptr fix_mpz_init()];
        let dtor = |ptr| (
            CALL_C[() fix_mpz_clear(Ptr), ptr]
        );
        MPZ { _data : Destructor::make(ptr, dtor) }
    );

    zero : MPZ;
    zero = MPZ::_make();

    borrow_ptr : (Ptr -> a) -> MPZ -> a;
    borrow_ptr = |proc, num| (
        let res = proc(num.@_data.@value);
        let _ = num; // use num
        res
    );

    _is_unique_data : MPZ -> (Bool, MPZ);
    _is_unique_data = |num| (
        let (is_unique, data) = num.@_data.is_unique;
        (is_unique, MPZ { _data : data })
    );

    // MPZ itelf is unboxed, but ...
    force_unique : MPZ -> MPZ;
    force_unique = |src| (
        let (is_unique, src) = src._is_unique_data;
        if is_unique { src };
        let dst = MPZ::_make();
        let _ = dst.borrow_ptr(|dst_ptr| (
            src.borrow_ptr(|src_ptr| (
                CALL_C[() fix_mpz_set(Ptr, Ptr), dst_ptr, src_ptr]
            ))
        ));
        dst
    );

    set_decimal : String -> MPZ -> Option MPZ;
    set_decimal = |dec, num| (
        let num = num.force_unique;
        num.borrow_ptr(|ptr| (
            let suc = dec.call_with_c_str(|c_str| (
                CALL_C[I64 fix_mpz_set_str(Ptr, Ptr, I64), ptr, c_str, 10]
            ));
            if suc == -1 { Option::none() };
            Option::some $ num
        ))
    );

    from_decimal : String -> Option MPZ;
    from_decimal = |dec| MPZ::zero.set_decimal(dec);

}

impl MPZ : ToString {

    to_string = |num| (
        num.borrow_ptr(|num_ptr| (
            let len_base = CALL_C[I64 fix_mpz_sizeinbase(Ptr, I64), num_ptr, 10];
            let len = len_base + 2;
            let buf = Array::fill(len, 0_U8);
            let _ = buf.call_with_ptr(|ptr| (
                CALL_C[Ptr fix_mpz_get_str(Ptr, I64, Ptr), ptr, 10, num_ptr]
            ));
            String::from_c_str(buf)
        ))
    );

}